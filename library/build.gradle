/*
 * Copyright (c) 2018. Jahir Fiquitiva
 *
 * Licensed under the CreativeCommons Attribution-ShareAlike
 * 4.0 International License. You may not use this file except in compliance
 * with the License. You may obtain a copy of the License at
 *
 *    http://creativecommons.org/licenses/by-sa/4.0/legalcode
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion versions.targetSdk
    buildToolsVersion versions.buildTools

    lintOptions {
        warningsAsErrors true
        disable 'MissingTranslation',
                'GoogleAppIndexingWarning',
                'UnusedResources',
                'ContentDescription',
                'RtlSymmetry',
                'RtlHardcoded',
                'RtlEnabled',
                'Overdraw'
    }

    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode versions.versionCode
        versionName versions.versionName
        vectorDrawables.useSupportLibrary = true
        buildConfigField("String", "LIB_VERSION", "\"${versions.versionName.toString()}\"")
        proguardFiles 'proguard-android-optimize.txt', 'proguard-rules.pro'
        consumerProguardFiles 'proguard-android-optimize.txt', 'proguard-rules.pro'
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles 'proguard-android-optimize.txt', 'proguard-rules.pro'
            consumerProguardFiles 'proguard-android-optimize.txt', 'proguard-rules.pro'
            multiDexKeepProguard file('proguard-rules.pro')
        }
    }

    aaptOptions {
        noCompress 'zw', 'zip', 'komp', 'klwp', 'kwgt', 'klck', 'kwch'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    packagingOptions {
        pickFirst 'META-INF/core_debug.kotlin_module'
        pickFirst 'META-INF/core_release.kotlin_module'
        pickFirst 'META-INF/library_debug.kotlin_module'
        pickFirst 'META-INF/library_release.kotlin_module'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    // Kuper
    api(libraries.kuper) {
        transitive = true
        exclude module: 'constraint'
        exclude module: 'transition'
        exclude module: 'mediarouter-v7'
        exclude module: 'support-media-compat'
    }
    // Fuel
    api libraries.fuel
    // CounterFab
    api(libraries.counterFab) {
        exclude group: 'com.android.support'
    }
}

configurations {
    all*.exclude module: 'constraint'
    all*.exclude module: 'transition'
    all*.exclude module: 'mediarouter-v7'
    all*.exclude module: 'support-media-compat'
    all*.exclude group: 'com.google.code'
}

tasks.whenTaskAdded { task ->
    if (task.name.contains("lint") || task.name.contains("test")) {
        task.enabled = false
    }
}

apply from: '../lib-artifacts.gradle'
